#
# A simple theme that displays relevant, contextual information.
#
# Authors:
#   Sorin Ionescu <sorin.ionescu@gmail.com> (`sorin` theme)
#   Ben Cox <cox@djehuti.com> (modified `sorin` -> `djehuti`)
#
# Screenshots:
#   TBD
#

#
# 16 Terminal Colors
# -- ---------------
#  0 black
#  1 red
#  2 green
#  3 yellow
#  4 blue
#  5 magenta
#  6 cyan
#  7 white
#  8 bright black
#  9 bright red
# 10 bright green
# 11 bright yellow
# 12 bright blue
# 13 bright magenta
# 14 bright cyan
# 15 bright white
#

# Load dependencies.
pmodload 'helper'

function prompt_djehutinogit_pwd {
  local pwd="${PWD/#$HOME/~}"

  if [[ "$pwd" == (#m)[/~] ]]; then
    _prompt_djehutinogit_pwd="$MATCH"
    unset MATCH
  else
    _prompt_djehutinogit_pwd="${${${${(@j:/:M)${(@s:/:)pwd}##.#?}:h}%/}//\%/%%}/${${pwd:t}//\%/%%}"
  fi
}

function prompt_djehutinogit_precmd {
  setopt LOCAL_OPTIONS
  unsetopt XTRACE KSH_ARRAYS

  # Format PWD.
  prompt_djehutinogit_pwd
}

function prompt_djehutinogit_setup {
  setopt LOCAL_OPTIONS
  unsetopt XTRACE KSH_ARRAYS
  prompt_opts=(cr percent subst)

  # Load required functions.
  autoload -Uz add-zsh-hook

  # Add hook for calling git-info before each command.
  add-zsh-hook precmd prompt_djehutinogit_precmd

  # Set editor-info parameters.
  zstyle ':prezto:module:editor:info:completing' format '%B%F{7}...%f%b'
  zstyle ':prezto:module:editor:info:keymap:primary' format '%(!.%F{1}#%f.%F{4}%%%f)'
  zstyle ':prezto:module:editor:info:keymap:primary:overwrite' format ' %F{3}♺%f'
  zstyle ':prezto:module:editor:info:keymap:alternate' format '%F{4}:%f'

  # Set git-info parameters.
  zstyle ':prezto:module:git:info' verbose 'yes'
  zstyle ':prezto:module:git:info:action' format '%{%F{7}%}:%{%f%%B%F{9}%}%s%{%f%%b%}'
  zstyle ':prezto:module:git:info:added' format ' %{%%B%F{2}%}✚%{%f%%b%}'
  zstyle ':prezto:module:git:info:ahead' format ' %{%%B%F{13}%}⬆%{%f%%b%}'
  zstyle ':prezto:module:git:info:behind' format ' %{%%B%F{13}%}⬇%{%f%%b%}'
  zstyle ':prezto:module:git:info:branch' format ' %{%%B%F{2}%}%b%{%f%%b%}'
  zstyle ':prezto:module:git:info:commit' format ' %{%%B%F{3}%}%.7c%{%f%%b%}'
  zstyle ':prezto:module:git:info:deleted' format ' %{%%B%F{1}%}✖%{%f%%b%}'
  zstyle ':prezto:module:git:info:modified' format ' %{%%B%F{4}%}✱%{%f%%b%}'
  zstyle ':prezto:module:git:info:position' format ' %{%%B%F{13}%}%p%{%f%%b%}'
  zstyle ':prezto:module:git:info:renamed' format ' %{%%B%F{5}%}➜%{%f%%b%}'
  zstyle ':prezto:module:git:info:stashed' format ' %{%%B%F{6}%}✭%{%f%%b%}'
  zstyle ':prezto:module:git:info:unmerged' format ' %{%%B%F{9}%}═%{%f%%b%}'
  zstyle ':prezto:module:git:info:untracked' format ' %{%%B%F{2}%}◼%{%f%%b%}'
  zstyle ':prezto:module:git:info:keys' format \
    'status' '$(coalesce "%b" "%p" "%c")%s%A%B%S%a%d%m%r%U%u'

  # Define prompts.
  PROMPT='%{%F{3}%}%!%{%f%} ${SSH_TTY:+"%{%F{9}%}%n@%m%{%f%} "}%{%F{4}%}${_prompt_djehutinogit_pwd}%{%f%}${editor_info[keymap]} '
  SPROMPT='zsh: correct %{%F{1}%}%R%{%f%} to %{%F{2}%}%r%{%f%} [nyae]? '
}

function prompt_djehutinogit_preview {
  local +h PROMPT=''
  local +h SPROMPT=''

  editor-info 2>/dev/null
  prompt_preview_theme 'djehutinogit'
}

prompt_djehutinogit_setup "$@"
